function() {
  
  # Priors for SR portion
  log_alpha ~ dnorm(0,0.01) %_% I(,3.5)
  beta ~ dunif(0,0.5)
  phi ~ dunif(-1, 0.99)
  tau_R_white ~ dgamma(0.01, 0.01)
  tau_R_red <- tau_R_white * (1 - phi * phi)
  log_resid_0 ~ dnorm(0, tau_R_red)
  sigma_R_white <- 1 / sqrt(tau_R_white)
  sigma_R_red <- 1 / sqrt(tau_R_red)
  alpha <- exp(log_alpha)
  
  ### PART 1: BIOLOGICAL PROCESS SUBMODEL: BROOD YEAR RECRUITMENT PROCESS #####
  
  #1a) Brood year returns without SR link; drawn from a common lognormal dist
  log_mean_R0 ~ dnorm(0,0.0001)#%_%I(0,)        
  tau_R0 ~ dgamma(0.1,0.1)      
  sigma_R0 <- 1 / sqrt(tau_R0)
  for(y in 1:a_max){ 
    log_R[y] ~ dnorm(log_mean_R0, tau_R0)   
    R[y] <- exp(log_R[y])
    log_resid[y] <- log_R[y] - log_mean_R0
  }
  
  #1b) Stock-Recruitment with Autocorrelated lag-1 residuals: for years with spawner/recruit link
  for (y in (na+a_min):ny) {
    log_R[y] ~ dnorm(log_Rmean2[y],tau_R_white)
    R[y] <- exp(log_R[y])
    log_Rmean1[y] <- log_alpha + log(eggs_t[y-a_max]) - beta * eggs_t[y-a_max]
    log_resid[y] <- log_R[y] - log_Rmean1[y]
  }
  log_Rmean2[na+a_min] <- log_Rmean1[na+a_min] + phi * log_resid_0
  for (y in (na+a_min+1):ny) {
    log_Rmean2[y] <- log_Rmean1[y] + phi * log_resid[y-1]
  }
  
  ### PART 2: MATURITY PROCESS SUBMODEL ###
  # 2a) brood-year specific sex ratio
  # sex_scale ~ dunif(0.01,1)
  # sex_sum <- 1/sex_scale^2
  b0_sex ~ dnorm(0,1e-6)
  b1_sex ~ dnorm(0,1e-6)
  for (y in 1:ny) {
    logit(mu_pi_f[y]) <- b0_sex + b1_sex * y
    # B1[y] <- mu_pi_f[y] * sex_sum
    # B2[y] <- (1 - mu_pi_f[y]) * sex_sum
    # p_f[y] ~ dbeta(B1[y], B2[y])
    # R_sex[y,1] <- R[y] * p_f[y]
    # R_sex[y,2] <- R[y] * (1 - p_f[y])
    R_sex[y,1] <- R[y] * mu_pi_f[y]
    R_sex[y,2] <- R[y] * (1 - mu_pi_f[y])
  }
  
  # 2b) brood year-specific maturity schedules by sex, with separate estimated time trends for each sex
  D_scale ~ dunif(0.01,1)
  D_sum <- 1/D_scale^2
  for (s in 1:2) {
    
    # set coefs for last age
    b0_mat[s,na] <- 1
    b1_mat[s,na] <- 0
    
    # estimate coefficients for the rest of the ages
    for (a in 1:(na-1)) {
      b0_mat[s,a] ~ dnorm(0,1e-6)
      b1_mat[s,a] ~ dnorm(0,1e-6)
    }
    
    # draw dirichlet random variables around the expectation
    for (y in 1:ny) {
      for (a in 1:na) {
        eta_mat[y,a,s] <- exp(b0_mat[s,a] + b1_mat[s,a] * y)
        mu_pi_mat[y,a,s] <- eta_mat[y,a,s] / sum(eta_mat[y,1:na,s])
        gamma[y,a,s] <- D_sum * mu_pi_mat[y,a,s]
        g[y,a,s] ~ dgamma(gamma[y,a,s],0.1)
        p[y,a,s] <- g[y,a,s]/sum(g[y,1:na,s])
      }
    }
  }
  
  ##### PART 3: HARVEST PROCESS MODEL #####
  # 3a) Apportion sex-specific recruits to each age and calendar year
  for (t in 1:nt) {
    for (a in 1:na) {
      for (s in 1:2) {
        N_tas[t,a,s] <- R_sex[t+na-a,s] * p[t+na-a,a,s]
      }
      N_ta[t,a] <- sum(N_tas[t,a,1:2])
    }
    N_t[t] <- sum(N_ta[t,1:na])
  }
  
  # 3b) Create age/sex/year/mesh-specific vulnerability/selectivity schedule
  Vtau ~ dnorm(1.920, 1/(0.012 * 10)^2)
  Vsig ~ dnorm(0.204, 1/(0.021 * 10)^2) %_% I(0,)
  Vtha ~ dnorm(0.622, 1/(0.033 * 10)^2) %_% I(0,)
  Vlam ~ dnorm(-0.547, 1/(0.075 * 10)^2)
  Vtau_prior ~ dnorm(1.920, 1/(0.012 * 10)^2)
  Vsig_prior ~ dnorm(0.204, 1/(0.021 * 10)^2) %_% I(0,)
  Vtha_prior ~ dnorm(0.622, 1/(0.033 * 10)^2) %_% I(0,)
  Vlam_prior ~ dnorm(-0.547, 1/(0.075 * 10)^2)
  Vtau_yukon ~ dnorm(1.920, 1/(0.012)^2)
  Vsig_yukon ~ dnorm(0.204, 1/(0.021)^2) %_% I(0,)
  Vtha_yukon ~ dnorm(0.622, 1/(0.033)^2) %_% I(0,)
  Vlam_yukon ~ dnorm(-0.547, 1/(0.075)^2)
  
  for (t in 1:nt) {
    for (s in 1:2) {
      for (m in 1:2) {
        for (a in 1:na) {
          # the pearson model
          v_term_1[t,a,s,m] <- (1 + Vlam^2/(4 * Vtha^2))^Vtha
          v_term_2[t,a,s,m] <- rlm[t,a,s,m] - (Vsig * Vlam)/(2 * Vtha) - Vtau
          v_term_3[t,a,s,m] <- (1 + v_term_2[t,a,s,m]^2/Vsig^2)^-Vtha
          v_term_4[t,a,s,m] <- exp(-Vlam * (atan(v_term_2[t,a,s,m]/Vsig) + atan(Vlam/(2 * Vtha))))
          v_raw[t,a,s,m] <- v_term_1[t,a,s,m] * v_term_3[t,a,s,m] * v_term_4[t,a,s,m]
          
          # standardize within year and mesh so one age/sex combo is fully vuln
          v[t,a,s,m] <- v_raw[t,a,s,m]/max(v_raw[t,1:na,1:2,m])
        }
      }
    }
  }
  
  # 3c: calculate harvest by age, sex, year, and fishery
  for (t in 1:nt) {
    Fsub[t] ~ dunif(0,10)
    Fcom[t] ~ dunif(0,10)
    for (a in 1:na) {
      for (s in 1:2) {
        Usub_tas[t,a,s] <- 1 - exp(-Fsub[t] * v[t,a,s,sub_mesh[t]])
        Ucom_tas[t,a,s] <- 1 - exp(-Fcom[t] * v[t,a,s,com_mesh[t]])
        
        Hsub_tas[t,a,s] <- N_tas[t,a,s] * Usub_tas[t,a,s]
        Hcom_tas[t,a,s] <- (N_tas[t,a,s] * (1 - Usub_tas[t,a,s])) * Ucom_tas[t,a,s]
        S_tas[t,a,s] <- N_tas[t,a,s] - Hsub_tas[t,a,s] - Hcom_tas[t,a,s]
        eggs_tas[t,a,s] <- S_tas[t,a,s] * fecund[t,a,s]
        Utot_tas[t,a,s] <- (N_tas[t,a,s] - S_tas[t,a,s])/N_tas[t,a,s]
      }
      Hsub_ta[t,a] <- sum(Hsub_tas[t,a,1:2])
      Hcom_ta[t,a] <- sum(Hcom_tas[t,a,1:2])
      S_ta[t,a] <- sum(S_tas[t,a,1:2])
      Utot_ta[t,a] <- (N_ta[t,a] - S_ta[t,a])/N_ta[t,a]
    }
    Hsub[t] <- sum(Hsub_ta[t,1:na])
    Hcom[t] <- sum(Hcom_ta[t,1:na])
    S_t[t] <- sum(S_ta[t,1:na])
    eggs_t[t] <- sum(eggs_tas[t,1:na,1:2])
    eggs_per_S_t[t] <- eggs_t[t]/S_t[t]
    eggs_per_female_t[t] <- eggs_t[t]/sum(S_tas[t,1:na,1])
    Utot_t[t] <- (N_t[t] - S_t[t])/N_t[t]
    for (s in 1:2) {
      Utot_ts[t,s] <- (sum(N_tas[t,1:na,s]) - sum(S_tas[t,1:na,s]))/sum(N_tas[t,1:na,s])
    }
  }
  
  ### PART 4: OBSERVATION SUB MODEL ###
  for (t in 1:nt) {
    S_obs[t] ~ dlnorm(log(S_t[t]), 1/S_obs_sig[t]^2)
    Hcom_obs[t] ~ dlnorm(log(Hcom[t]), 1/Hcom_obs_sig[t]^2)
    Hsub_obs[t] ~ dlnorm(log(Hsub[t]), 1/Hsub_obs_sig[t]^2)
    
    # turn age/sex/year structured 3-d arrays into 2-d
    # [1:na] is females (s == 1); [(na+1):(2*na)] is males
    # allows fitting multinomial more easily
    
    N_tas_2d[t,1:na] <- N_tas[t,1:na,1]
    N_tas_2d[t,(na+1):(2*na)] <- N_tas[t,1:na,2]
    
    S_tas_2d[t,1:na] <- S_tas[t,1:na,1]
    S_tas_2d[t,(na+1):(2*na)] <- S_tas[t,1:na,2]
    
    Hcom_tas_2d[t,1:na] <- Hcom_tas[t,1:na,1]
    Hcom_tas_2d[t,(na+1):(2*na)] <- Hcom_tas[t,1:na,2]
    
    Hsub_tas_2d[t,1:na] <- Hsub_tas[t,1:na,1]
    Hsub_tas_2d[t,(na+1):(2*na)] <- Hsub_tas[t,1:na,2]
    
    for (as in 1:(na*2)) {
      q_esc[t,as] <- S_tas_2d[t,as]/S_t[t]
      q_com[t,as] <- Hcom_tas_2d[t,as]/Hcom[t]
      q_sub[t,as] <- Hsub_tas_2d[t,as]/Hsub[t]
      q_run[t,as] <- N_tas_2d[t,as]/N_t[t]
    }
    
    x_esc[t,1:(2*na)] ~ dmulti(q_esc[t,1:(2*na)], n_esc[t])
    x_com[t,1:(2*na)] ~ dmulti(q_com[t,1:(2*na)], n_com[t])
    x_sub[t,1:(2*na)] ~ dmulti(q_sub[t,1:(2*na)], n_sub[t])
  }
}
